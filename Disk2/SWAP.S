 LST OFF
*
* PCS - SWAPPING HANDLER
*
 ORG $4A00
 OBJ $8000
*
TEMP EQU $C7
XTEMP EQU TEMP+1
YTEMP EQU XTEMP+1
*
DSTATS EQU $303
DBUFR EQU $304
DSCTR EQU $30A
*
* EXTERNALS
*
DIV7 EQU $2100
MOD7 EQU DIV7+$100
LO EQU MOD7+$100
HI EQU LO+$C0
SETMODE EQU HI+$10F
DRAWBITS EQU SETMODE+$25
XOFFDRAW EQU DRAWBITS+$41
GETRECT EQU XOFFDRAW+$5B
MASKS EQU GETRECT+$41
HLINE EQU MASKS+$11
VLINE EQU HLINE+$20
FRAMERECT EQU VLINE+$58
DRAWRECT EQU FRAMERECT+$28
INRECT EQU DRAWRECT+$10
GETBUTNS EQU INRECT+$2D
INITCRSR EQU GETBUTNS+$B
XDRAWCRSR EQU INITCRSR+$14
UPDATECRSR EQU XDRAWCRSR+$7
DOCRSRX EQU UPDATECRSR+$1E
GETCURSORX EQU DOCRSRX+$17
DOCRSRY EQU GETCURSORX+$10
WAIT EQU DOCRSRY+$3
CRSRINRECT EQU WAIT+$C
DOMENU EQU CRSRINRECT+$13
SELECT EQU DOMENU+$54
INIT EQU SELECT+$40
MOVEUP EQU INIT+$35
MOVEDOWN EQU MOVEUP+$3B
ADDIYX EQU MOVEDOWN+$39
ADDYX EQU ADDIYX+$E
SUBIYX EQU ADDYX+$10
SUBYX EQU SUBIYX+$12
CMPYX EQU SUBYX+$10
CHARTO EQU CMPYX+$F
PRCHAR EQU CHARTO+$A
PRINT EQU PRCHAR+$38
CHAR EQU PRINT+$19
*
VALIDBUF EQU $BF00
BITMAPS EQU $7B00
CHECKPAGE EQU $7B00
GETSECTORS EQU $06A0
EDITOR EQU $A000
*
SWAPWIRE LDX #1
 JSR SWAPPIECE
 JMP EDITOR+$13
*
SWAPDISK LDX #2
 JSR SWAPPIECE
 JMP EDITOR+$10
*
SWAPUSER LDX #3
 BNE SWAPSECTORS
*
RELOAD PHA ;SAVE STACK
 JSR PCSINSERTED
 LDA #0
 STA DBUFR
 STA DSCTR+1
 LDA #>BITMAPS
 LDX #$C0
 LDY #$70
 JSR GETSECTORS
 PLA
 TAX
 TXS
 RTS
*
SWAPPIECE JSR SWAPSECTORS
 JSR EDITOR
 LDX #0
*
SWAPSECTORS TXA
 PHA
 JSR PCSINSERTED
 PLA
 TAX
*
 LDA #0
 STA DBUFR
 LDA SECTORHI,X
 STA DSCTR+1
 LDA ADDRHI,X
 PHA
 LDY COUNT,X
 LDA SECTOR,X
 TAX
 PLA
 JMP GETSECTORS
*
COUNT HEX 26141014
SECTORHI HEX 01010101
SECTOR HEX 0A304454
ADDRHI HEX A0A0A090
*
PCSINSERTED LDA #0
 STA DBUFR
 STA DSCTR+1
 LDA #>VALIDBUF
 LDX #$C0
 LDY #1
 JSR GETSECTORS
 LDA DSTATS
 CMP #1
 BNE PCSINSERTED
*
 LDY #$40
PCSIN2 LDA VALIDBUF,Y
 CMP CHECKPAGE,Y
 BNE PCSIN3
 DEY
 BPL PCSIN2
 RTS
*
PCSIN3 LDY #1
 JSR SETMODE
 LDA #<DIALOGBITS
 LDX #>DIALOGBITS
 JSR DRAWBITS
 LDY #3
 JSR SETMODE
 LDA #<DIALOGBOX
 LDX #>DIALOGBOX
 JSR DRAWRECT
 LDY #2
 JSR SETMODE
 JSR FRAMERECT+3
*
 LDY #$59
 LDX #1
 LDA #0
 JSR CHARTO
 LDA #<INSERTMSG
 LDX #>INSERTMSG
 JSR PRINT
*
 LDA #$FF
 STA $2FC
WAIT1 LDA $2FC
 CMP #$FF
 BEQ WAIT1
 LDA #$FF
 STA $2FC
*
 LDY #0
 JSR SETMODE
 LDA #<DIALOGBITS
 LDX #>DIALOGBITS
 JSR DRAWBITS
 LDY #2
 JSR SETMODE
*
 JMP PCSINSERTED
*
INSERTMSG HEX 12171C0E1B1D24190C1C240D121C94
*
DIALOGBITS DA $B500 ;SAFE AREA
 HEX 5700000B28
*
DIALOGBOX HEX 5700000A2707
*
