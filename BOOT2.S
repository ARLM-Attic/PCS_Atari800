 LST OFF
*
* DUMMY PCS BOOT SECTOR
*  FOR ELECTRONIC ARTS
*
GTSCTR EQU $52
*
DUNIT EQU $301
DCOMND EQU $302
DSTATS EQU $303
DBUFR EQU $304
DSCTR EQU $30A
*
INTERRUPTS EQU $49DC
GOATARI EQU $4B45
PIECE2 EQU $7B00 ;AFTER TST.O
EDITOR EQU $A000
DISKINV EQU $E453
*
 ORG $680
 OBJ $7000
*
 HEX 00 ;BOOT SECTOR
 HEX 8A
 DA $680
 DA INIT
*
INIT JSR GOATARI
 JSR INTERRUPTS
STAGE2 LDA #0 ;GET EDITOR
 STA DSCTR+1
 STA DBUFR
 LDA #>PIECE2
 LDX #$C0
 LDY #$70
 JSR GETSECTORS
 JMP EDITOR
*
GETSECTORS STA DBUFR+1
 STX DSCTR
 STY SECTORCOUNT
*
 LDA #1
 STA DUNIT
 LDA #GTSCTR
 STA DCOMND
*
GETS2 JSR DISKINV
 LDA DSTATS
 CMP #1
 BNE GETS2
*
 LDA DBUFR
 ADC #127 ;C=1!
 STA DBUFR
 LDA DBUFR+1
 ADC #0
 STA DBUFR+1
 INC DSCTR
 BNE GETS3
 INC DSCTR+1
GETS3 DEC SECTORCOUNT
 BNE GETS2
NULL RTS
*
SECTORCOUNT HEX 00
