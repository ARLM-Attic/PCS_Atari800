 LST OFF
*
* PCS DISK I/O AND KEYBOARD
*  HANDLERS
*
 ORG $A000
 OBJ $8000
*
* CONSTANTS
*
OPEN EQU 3
GETCHR EQU 6
PUTCHR EQU 10
CLOSE EQU 12
*
PARAM EQU $C0
TEMP EQU $C7
XTEMP EQU TEMP+1
YTEMP EQU XTEMP+1
TEMP2 EQU YTEMP+1
*
BASE1 EQU $D0
BASE2 EQU BASE1+2
*
* USER INTERFACE
*
CURSOR EQU $80
CURSORY EQU CURSOR+2
CURSORXDIV7 EQU CURSORY+1
CURSORXMOD7 EQU CURSORXDIV7+1
CURSORHEIGHT EQU CURSORXMOD7+1
CURSORWIDTH EQU CURSORHEIGHT+1
NEWCURSORXDIV7 EQU CURSORWIDTH+1
NEWCURSORXMOD7 EQU NEWCURSORXDIV7+1
NEWCURSORY EQU NEWCURSORXMOD7+1
NEWITEM EQU NEWCURSORY+1
LASTITEM EQU NEWITEM+2
*
* DATA STRUCTURE
*
OBJ EQU LASTITEM+2
NEXTOBJ EQU OBJ+2
OBJCOUNT EQU NEXTOBJ+1
PLYPTRX EQU OBJCOUNT+1
PLYPTRY EQU PLYPTRX+2
OBJID EQU PLYPTRY+2
FILLCOLOR EQU OBJID+1
VRTXCOUNT EQU FILLCOLOR+1
LBASE EQU VRTXCOUNT+1
SCANMODE EQU LBASE+2
MEMBTM EQU SCANMODE+1
MIDBTM EQU MEMBTM+2
MIDTOP EQU MIDBTM+2
MIDY EQU MIDTOP+2
*
CHARINDX EQU MIDY+1
CHARASCII EQU CHARINDX+1
CHCODE EQU CHARASCII+1
BOXCURSORON EQU CHCODE+1
DTIMER1 EQU BOXCURSORON+1
DTIMER2 EQU DTIMER1+1
DISKCMD EQU DTIMER2+1
SAVETYPE EQU DISKCMD+1
BSPARM EQU SAVETYPE+1
STACKTEMP EQU BSPARM+4
*
GAMEBTM EQU $2B00
HIRES EQU $2B00
HTOP EQU $490F
GAMESTART EQU $9048
PLAYGAME EQU GAMESTART+26
PATCH EQU $99B5
GAMETOP EQU $A100
*
CH EQU $2FC
SHFLOK EQU $2BE
*
IOCB EQU $340
ICCOM EQU IOCB+2
ICBA EQU IOCB+4
ICBL EQU IOCB+8
ICAX1 EQU IOCB+10
FMINIT EQU $788
CIO EQU $E456
*
* PBDB
*
PBBASE EQU $4B00
LOGIC EQU PBBASE
WSET EQU LOGIC+24
PBDATA EQU WSET+4
OBJDX EQU PBDATA+1
PBDX EQU $7A40
*
* EXTERNALS
*
DIV7 EQU $2100
MOD7 EQU DIV7+$100
LO EQU MOD7+$100
HI EQU LO+$C0
SETMODE EQU HI+$10F
DRAWBITS EQU SETMODE+$25
XOFFDRAW EQU DRAWBITS+$41
GETRECT EQU XOFFDRAW+$5B
MASKS EQU GETRECT+$41
HLINE EQU MASKS+$11
VLINE EQU HLINE+$20
FRAMERECT EQU VLINE+$58
DRAWRECT EQU FRAMERECT+$28
INRECT EQU DRAWRECT+$10
GETBUTNS EQU INRECT+$2D
INITCRSR EQU GETBUTNS+$B
XDRAWCRSR EQU INITCRSR+$14
UPDATECRSR EQU XDRAWCRSR+$7
DOCRSRX EQU UPDATECRSR+$1E
GETCURSORX EQU DOCRSRX+$17
DOCRSRY EQU GETCURSORX+$10
WAIT EQU DOCRSRY+$3
CRSRINRECT EQU WAIT+$C
DOMENU EQU CRSRINRECT+$13
SELECT EQU DOMENU+$54
INIT EQU SELECT+$40
MOVEUP EQU INIT+$35
MOVEDOWN EQU MOVEUP+$3B
ADDIYX EQU MOVEDOWN+$39
ADDYX EQU ADDIYX+$E
SUBIYX EQU ADDYX+$10
SUBYX EQU SUBIYX+$12
CMPYX EQU SUBYX+$10
CHARTO EQU CMPYX+$F
PRCHAR EQU CHARTO+$A
PRINT EQU PRCHAR+$38
CHAR EQU PRINT+$19
*
* PPAK
*
DRAWDISPLAY EQU $9920
GETOBJ EQU DRAWDISPLAY+$3E
GETNEXTOBJ EQU GETOBJ+$C
GETINFO EQU GETNEXTOBJ+$1B
DRAWOBJ EQU GETINFO+$3E
SCANPOLY EQU DRAWOBJ+$14
ALIGNPOLY EQU SCANPOLY+$433
GETSCAN EQU ALIGNPOLY+$39
MAKEHOLE EQU GETSCAN+$57
SELECTPOLY EQU MAKEHOLE+$4A
POLYPOINTS EQU SELECTPOLY+$33
REMOVEPOLY EQU POLYPOINTS+$37
GETBOUNDS EQU REMOVEPOLY+$6E
*
SWAPUSER EQU $4A10
RELOAD EQU $4A14
*
 TSX
 STX STACKTEMP
 JSR FMINIT
*
START LDY #2
 JSR SETMODE
 LDA #$40
 STA SHFLOK
*
 LDY #0
 LDA #$4D
SETUP2 STY XTEMP
 STA CHAR+2
 LDA #$15
 STA CHAR+3
 LDA #0
 STA CHAR+4
 LDA TXTLO,Y
 LDX TXTHI,Y
 JSR PRINT
 LDA CHAR+2
 CLC
 ADC #10
 LDY XTEMP
 CPY #2
 BNE *+4
 ADC #10
 CPY #4
 BNE *+4
 ADC #10
 INY
 CPY #7
 BNE SETUP2
*
 LDA #<HAND
 LDX #>HAND
 JSR INITCRSR
*
MAIN JSR UPDATECRSR
 JSR GETBUTNS
 BPL MAIN
*
MAIN4 LDA #<MENUBOX
 LDX #>MENUBOX
 JSR CRSRINRECT
 BCC MAIN5
*
 LDA #0
 STA LASTITEM+1
 LDA #<DISKMENU
 LDX #>DISKMENU
 JSR DOMENU
MAIN5 JMP MAIN
*
HAND DA *+7
 HEX 0000000C02
 HEX C000600030001860
 HEX 2CC03EC07FC07FC0
 HEX 3FA01F600EC00180
 HEX 00
*
DISKMENU DA LOADBOX
 DA LOAD
 DA SAVEBOX
 DA SAVE
 DA QUITBOX
 DA QUIT
 DA MAKEBOX
 DA MAKE
 DA PLAYBOX
 DA PLAY
 HEX 00
*
TXTLO DA <LOADMSG
 DA <SAVEMSG
 DA <QUITMSG
 DA <MAKEMSG
 DA <GAMEMSG
 DA <PLAYMSG
 DA <GAMEMSG
*
TXTHI DA >LOADMSG
 DA >SAVEMSG
 DA >QUITMSG
 DA >MAKEMSG
 DA >GAMEMSG
 DA >PLAYMSG
 DA >GAMEMSG
*
LOADMSG HEX 15180A8D
SAVEMSG HEX 1C0A1F8E
QUITMSG HEX 1A1E129D
MAKEMSG HEX 160A148E
GAMEMSG HEX 100A168E
PLAYMSG HEX 19150AA2
INSERTMSG HEX 12171C0E1B1D2422181E1B24100A160E240D121C14
 HEX 240A170D2411121D240A24140EA2
*
MENUBOX HEX 401400680F06
LOADBOX HEX 4B14040A0400
SAVEBOX HEX 5514040A0400
QUITBOX HEX 5F14040A0400
MAKEBOX HEX 731404140402
PLAYBOX HEX 931404140402
*
LOAD JSR GETNAME
 JSR INPROMPT1
 LDA #0
 STA DISKCMD
*
 LDX #$10 ;POINT TO FILENAME
 LDA #<CHARBUF
 STA ICBA,X
 LDA #>CHARBUF
 STA ICBA+1,X
*
 LDA #OPEN ;OPEN IT
 STA ICCOM,X
 LDA #4
 STA ICAX1,X
 JSR CIO
 BPL RDLF
 LDA #1
 BNE CLFX
*
RDLF LDX #$10 ;GET $84,$09
 LDA #<DBUF
 STA ICBA,X
 LDA #>DBUF
 STA ICBA+1,X
 LDA #2
 STA ICBL,X
 LDA #0
 STA ICBL+1,X
 LDA #GETCHR
 STA ICCOM,X
 JSR CIO
 BMI ERST
 LDA #$FF
 CMP DBUF
 BNE LNLF
 LDA #$FF
 CMP DBUF+1
 BNE LNLF
*
RDDRC LDX #$10 ;GET $ADDR,$LEN
 LDA #<DBUF
 STA ICBA,X
 LDA #>DBUF
 STA ICBA+1,X
 LDA #4
 STA ICBL,X
 LDA #0
 STA ICBL+1,X
 JSR CIO
 BPL STOK
 CPY #$88
 BNE ERST
*
 LDA #0
CLFX PHA
 TYA
 PHA
 LDX #$10
 LDA #CLOSE
 STA ICCOM,X
 JSR CIO
 PLA
 TAY
 PLA
 TAX
 JMP LOAD2
*
LNLF LDA #3
 BNE CLFX
ERST LDA #2
 BNE CLFX
*
STOK LDX #$10 ;READ THE RECORD
 LDA DBUF
 STA ICBA,X
 LDA DBUF+1
 STA ICBA+1,X
 LDA DBUF+2
 SEC
 SBC DBUF
 STA ICBL,X
 LDA DBUF+3
 SBC DBUF+1
 STA ICBL+1,X
 INC ICBL,X
 BNE *+5
 INC ICBL+1,X
 JSR CIO
 BMI ERST
 JMP RDDRC
*
LOAD2 CPX #0
 BEQ LOAD3
 CPX #3
 BEQ NLF
 TYA
 JMP CIOER
*
NLF LDA #<BLFMSG
 LDX #>BLFMSG
 JMP PRINTERR
*
LOAD3 JSR INIT
 LDY PBDATA
 JSR GETOBJ
 JSR DECOMPRESS
 LDA #0
 STA SCANMODE
 JSR DRAWDISPLAY
RESETUP LDX STACKTEMP
 TXS
 JMP START
*
SAVE JSR GETNAME
 JSR INPROMPT1
 LDA #1
 STA DISKCMD
 LDA #$80
 STA SCANMODE
 JSR DRAWDISPLAY
 JSR COMPRESS
*
 LDA #<PBBASE
 STA BSPARM
 LDA #>PBBASE
 STA BSPARM+1
 LDA MIDBTM
 STA BSPARM+2
 LDA MIDBTM+1
 STA BSPARM+3
 LDA #8
 STA SAVETYPE
 JSR BSAVE
*
SAVE2 LDA #$00
 STA SCANMODE
 JSR DRAWDISPLAY
 JMP RESETUP
*
BSAVE LDX #$10
 LDA #<CHARBUF
 STA ICBA,X
 LDA #>CHARBUF
 STA ICBA+1,X
*
 LDA #OPEN ;OPEN IT
 STA ICCOM,X
 LDA SAVETYPE
 STA ICAX1,X
 JSR CIOCL
*
 LDX #$10 ;SAVE HEADER
 LDA #PUTCHR
 STA ICCOM,X
*
 LDA SAVETYPE
 CMP #9
 BEQ WDR ;APPEND, NO HEADER
*
 LDA #<SAVEHDR
 STA ICBA,X
 LDA #>SAVEHDR
 STA ICBA+1,X
 LDA #2
 STA ICBL,X
 LDA #0
 STA ICBL+1,X
 JSR CIOCL
*
WDR LDX #$10 ;SAVE $ADDR,$LEN
 LDA #<DATAHDR
 STA ICBA,X
 LDA #>DATAHDR
 STA ICBA+1,X
 LDA BSPARM
 STA DATAHDR
 LDA BSPARM+1
 STA DATAHDR+1
 LDA BSPARM+2
 SEC
 SBC #1
 STA DATAHDR+2
 LDA BSPARM+3
 SBC #0
 STA DATAHDR+3
*
 LDA #4
 STA ICBL,X
 LDA #0
 STA ICBL+1,X
 JSR CIOCL
*
 LDX #$10 ;SAVE DATA
 LDA BSPARM
 STA ICBA,X
 LDA BSPARM+1
 STA ICBA+1,X
 LDA BSPARM+2
 SEC
 SBC BSPARM
 STA ICBL,X
 LDA BSPARM+3
 SBC BSPARM+1
 STA ICBL+1,X
 JSR CIOCL
*
 LDX #$10
 LDA #CLOSE
 STA ICCOM,X
* JMP CIOCL
*
CIOCL JSR CIO
 TYA
 BMI *+3
 RTS
CIOER1 TYA
CIOER SEC
 SBC #100
 LDX #$FF
CTNS INX
 SEC
 SBC #10
 BPL CTNS
 CLC
 ADC #$8A
 STX ERTENS
 STA ERUNITS
 LDA #<ERRMSG
 LDX #>ERRMSG
*
PRINTERR PHA
 TXA
 PHA
 LDX #$10
 LDA #CLOSE
 STA ICCOM,X
 JSR CIO
*
ERROR LDA DISKCMD
 CMP #1
 BNE ERR2
 STA SCANMODE
 JSR DRAWDISPLAY
*
ERR2 JSR DODIALOG
 LDY #$59
 LDX #1
 LDA #1
 JSR CHARTO
 PLA
 TAX
 PLA
 JSR PRINT
*
 LDA #$FF
 STA CH
ERR7 CMP CH
 BEQ ERR7
*
 JSR UNDODIALOG
 LDA DISKCMD
 CMP #2
 BNE *+5
 JMP MAKEOUT
 JMP RESETUP
*
ERRMSG HEX 0E1B1B181B24242401
ERTENS HEX 00
ERUNITS HEX 80
*
BLFMSG HEX 0B0A0D2415180A0D240F12158E
*
QUIT LDX STACKTEMP
 TXS
 RTS
*
MAKE JSR GETNAME
 LDY CHARINDX
 LDA #$9B ;CR
 STA CHARBUF+3,Y
*
 LDA #2
 STA DISKCMD
 JSR CLEARMENU
 LDY #191
 JSR MAKEHOLE
 JSR SWAPUSER
 JSR INPROMPT1
*
 LDA #$80
 STA BASE1
 LDA #$24
 STA BASE1+1
 LDA #<PATCH
 STA BASE2
 LDA #>PATCH
 STA BASE2+1
 LDY #0
MAK2 LDA (BASE1),Y
 STA (BASE2),Y
 INY
 BNE MAK2
 INC BASE1+1
 INC BASE2+1
 LDA BASE1+1
 CMP #$2B
 BNE MAK2
*
 LDA #<GAMEBTM
 STA BSPARM
 LDA #>GAMEBTM
 STA BSPARM+1
 LDA #<GAMETOP
 STA BSPARM+2
 LDA #>GAMETOP
 STA BSPARM+3
 LDA #8
 STA SAVETYPE
 JSR BSAVE
*
 LDA #<GAMESTART
 STA $2E0 ;VECTOR
 LDA #>GAMESTART
 STA $2E1
 LDA #$E0
 STA BSPARM
 LDA #$02
 STA BSPARM+1
 LDA #$E2
 STA BSPARM+2
 LDA #$02
 STA BSPARM+3
 LDA #9 ;APPEND
 STA SAVETYPE
 JSR BSAVE
*
MAKEOUT LDA STACKTEMP
 JMP RELOAD
*
INPROMPT1 JSR DODIALOG
 LDY #$59
 LDX #1
 LDA #0
 JSR CHARTO
 LDA #<INSERTMSG
 LDX #>INSERTMSG
 JSR PRINT
*
WAIT1 LDA CH
 CMP #$FF
 BEQ WAIT1
 JMP UNDODIALOG
*
PLAY JSR CLEARMENU
 LDY #191
 JSR MAKEHOLE
 JSR SWAPUSER
 JSR PLAYGAME
 JMP MAKEOUT
*
CLEARMENU JSR XDRAWCRSR
 LDY #3
 JSR SETMODE
 LDA #<MENUBOX
 LDX #>MENUBOX
 JSR DRAWRECT
 LDY #2
 JMP SETMODE
*
DODIALOG LDY #1
 JSR SETMODE
 LDA #<DIALOGBITS
 LDX #>DIALOGBITS
 JSR DRAWBITS
 LDY #3
 JSR SETMODE
 LDA #<DIALOGBOX
 LDX #>DIALOGBOX
 JSR DRAWRECT
 LDY #2
 JSR SETMODE
 JMP FRAMERECT+3
*
UNDODIALOG LDY #0
 JSR SETMODE
 LDA #<DIALOGBITS
 LDX #>DIALOGBITS
 JSR DRAWBITS
 LDY #2
 JMP SETMODE
*
GETNAME JSR CLEARMENU
 JSR DODIALOG
 LDA #$44 ;"D1:"
 STA CHARBUF
 LDA #$31
 STA CHARBUF+1
 LDA #$3A
 STA CHARBUF+2
*
 LDA #$FF
 STA CH
*
 LDY #$59
 LDX #$01
 LDA #0
 STX BOXCURSOR+3
 STA BOXCURSOR+4
 STA BOXCURSORON
 STA CHARINDX
 JSR CHARTO
*
GETN3 JSR CHECKCURSOR
 LDA CH
 CMP #$FF
 BEQ GETN3
*
 CMP #$C0
 BCS GETN3
*
FIX1 CMP #$40
 BCC FIX2
 SBC #$40
 BCS FIX1
*
FIX2 TAX
 LDA #$FF
 STA CH
 LDA ATASCII,X
*
 STA CHARASCII
 STX CHCODE
 CPX #$1C ;ESC
 BEQ GETN4
 CPX #$0C ;RETURN
 BEQ GETN5 ;C=1
 JSR CURSOROFF
 LDA CHARASCII
 LDX CHCODE
 JSR DOKBD
 JSR CURSOROFF
 JMP GETN3
*
GETN4 CLC
GETN5 PHP
 JSR CURSOROFF
*
 JSR UNDODIALOG
 LDY CHARINDX
 LDA #$2E
 STA CHARBUF+3,Y
 LDA #$50
 STA CHARBUF+4,Y
 LDA #$42
 STA CHARBUF+5,Y
 LDA #$9B
 STA CHARBUF+6,Y
 LDA #0
 STA CHARBUF+7,Y
 PLP
 BCS GETN6
 LDX STACKTEMP
 TXS
 JMP START
GETN6 RTS
*
DIALOGBITS DA $B500
 HEX 5700000B28
*
DIALOGBOX HEX 5700000A2706
*
DOKBD LDY CHARINDX
 CPX #$34 ;BACKSP
 BEQ DOKB7
*
 CPY #8
 BCS DOKB5
*
 CPX #$21
 BNE DOKB2
*
 STA CHARBUF+3,Y
 LDA CHAR+3
 STA LINELEN,Y
 TAX
 LDA CHAR+4
 STA LINELEN+9,Y
 CLC
 ADC #4
 CMP #8
 BCC *+5
 SBC #8
 INX
 STX CHAR+3
 STX BOXCURSOR+3
 STA CHAR+4
 STA BOXCURSOR+4
 JMP DOKB4
*
DOKB2 CMP #$30
 BCC DOKB5
 CMP #$3A
 BCS DOKB6
 SBC #$2F ;C=0, -10
*
DOKB3 TAX
 LDA CHARASCII
 STA CHARBUF+3,Y
 LDA CHAR+3
 STA LINELEN,Y
 LDA CHAR+4
 STA LINELEN+9,Y
 TXA
 JSR PRCHAR
 LDA CHAR+3
 STA BOXCURSOR+3
 LDA CHAR+4
 STA BOXCURSOR+4
DOKB4 INC CHARINDX
DOKB5 RTS
*
DOKB6 AND #$DF
 STA CHARASCII
 CMP #$41
 BCC DOKB5
 CMP #$5B
 BCS DOKB5
 SBC #$36 ;C=0 -B7
 BNE DOKB3
*
DOKB7 DEY
 BMI DOKB5
 STY CHARINDX
 LDA LINELEN,Y
 STA CHAR+3
 STA CLRRECT+1
 STA BOXCURSOR+3
 LDA LINELEN+9,Y
 STA CHAR+4
 STA CLRRECT+2
 STA BOXCURSOR+4
 LDA #$20
 STA CHARBUF+3,Y
 LDY #3
 JSR SETMODE
 LDA #<CLRRECT
 LDX #>CLRRECT
 JSR DRAWRECT
 LDY #2
 JMP SETMODE
*
CLRRECT HEX 590000070007
*
CURSOROFF LDA BOXCURSORON
 BNE DOCURSOR
 RTS
*
CHECKCURSOR INC DTIMER1
 BNE DOCR2
 INC DTIMER2
 LDA DTIMER2
 AND #$1F
 BNE DOCR2
 LDA BOXCURSORON
 EOR #$FF
 STA BOXCURSORON
*
DOCURSOR LDA #<BOXCURSOR
 LDX #>BOXCURSOR
 JMP XOFFDRAW
*
DOCR2 RTS
*
BOXCURSOR DA *+7
 HEX 5900000701
 HEX FCFCFCFCFCFCFC
*
COMPRESS LDA #<HIRES
 STA BASE1
 LDA #>HIRES
 STA BASE1+1
*
FINDZ LDY #0
FINDZ2 LDA (BASE1),Y
 BNE FINDZ3
 INY
 BNE FINDZ2
*
 LDA #1
 STA (MIDBTM),Y
 TYA
 INY
 STA (MIDBTM),Y ;1/256 RECORD
*
 INC BASE1+1 ;BASE+256
 LDA MIDBTM ;MIDBTM+2
 CLC
 ADC #2
 STA MIDBTM
 BCC *+4
 INC MIDBTM+1
 JMP FINDZ
*
FINDZ3 LDA BASE1+1
 CMP #>HTOP
 BCS FINDZ4
 CPY #2
 BCC FINDNZ
FINDZ4 STY TEMP
 LDA #1
 LDY #0
 STA (MIDBTM),Y
 INY
 LDA TEMP
 STA (MIDBTM),Y ;1/RUNLEN REC
*
 CLC
 ADC BASE1 ;BASE+RUNLEN
 STA BASE1
 BCC *+4
 INC BASE1+1
 LDA MIDBTM ;MIDBTM+2
 CLC
 ADC #2
 STA MIDBTM
 BCC *+4
 INC MIDBTM+1
*
 LDA BASE1+1
 CMP #>HTOP
 BCC FINDZ
 LDY #0
 LDA #1
 STA (MIDBTM),Y
 INY
 STA (MIDBTM),Y ;1/1 REC
*
 LDA MIDBTM
 CLC
 ADC #2
 STA MIDBTM
 BCC *+4
 INC MIDBTM+1
 RTS
*
FINDNZ INY
FINDNZ2 LDA (BASE1),Y
 BEQ FINDNZ5
 INY
 BNE FINDNZ2
*
FINDNZ3 TYA
 STA (MIDBTM),Y
 INC MIDBTM
 BNE *+4
 INC MIDBTM+1
*
FINDNZ4 LDA (BASE1),Y
 STA (MIDBTM),Y
 INY
 BNE FINDNZ4 ;256/BYTES REC
*
 INC BASE1+1
 INC MIDBTM+1
 JMP FINDZ
*
FINDNZ5 INY
 BEQ FINDNZ3
 LDA (BASE1),Y
 BNE FINDNZ2
 STY TEMP
 TYA
 LDY #0
 STA (MIDBTM),Y
 INC MIDBTM
 BNE *+4
 INC MIDBTM+1
*
FINDNZ6 LDA (BASE1),Y
 STA (MIDBTM),Y
 INY
 BNE FINDNZ6 ;RUN LEN/BYTES REC
*
 LDA TEMP
 CLC
 ADC BASE1
 STA BASE1
 BCC *+4
 INC BASE1+1
 LDA TEMP
 CLC
 ADC MIDBTM
 STA MIDBTM
 BCC *+4
 INC MIDBTM+1
 JMP FINDZ
*
DECOMPRESS LDA #<HIRES
 STA BASE1
 LDA #>HIRES
 STA BASE1+1
*
DCMP2 LDY #0
 LDA (OBJ),Y
 CMP #1
 BNE DCMP6
*
 INY
 LDA (OBJ),Y
 CMP #1
 BEQ DCMP5
 STA TEMP
 TAY
 DEY
*
 LDA #0
DCMP3 STA (BASE1),Y
 DEY
 BNE DCMP3
 STA (BASE1),Y
*
 LDA TEMP
 BEQ DCMP4
 CLC
 ADC BASE1
 STA BASE1
 BCC *+4
DCMP4 INC BASE1+1
 LDA OBJ
 CLC
 ADC #2
 STA OBJ
 BCC *+4
 INC OBJ+1
 JMP DCMP2
*
DCMP5 RTS
*
DCMP6 STA TEMP
 INC OBJ
 BNE *+4
 INC OBJ+1
 TAY
 DEY
*
DCMP7 LDA (OBJ),Y
 STA (BASE1),Y
 DEY
 BNE DCMP7
 LDA (OBJ),Y
 STA (BASE1),Y
*
 LDA TEMP
 BEQ DCMP8
 CLC
 ADC BASE1
 STA BASE1
 BCC *+4
DCMP8 INC BASE1+1
 LDA TEMP
 BEQ DCMP9
 CLC
 ADC OBJ
 STA OBJ
 BCC *+4
DCMP9 INC OBJ+1
 JMP DCMP2
*
ATASCII HEX 4C4A3A80804B5C5E
 HEX 4F8050559B495F7C
 HEX 568043808042585A
 HEX 348033361B353231
 HEX 5B205D4E804D3F81
 HEX 528045599F545751
 HEX 398030379C387D9D
 HEX 4648448083475341
*
SAVEHDR HEX FFFF
DATAHDR HEX 00000000
DBUF HEX 00000000
*
CHARBUF HEX 44313A00000000000000000000000000
LINELEN HEX 000000000000000000
